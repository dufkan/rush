WHITESPACE = _{ " " | "\t" | "\n" }
separator = { ";" | "&&" | "||" }
non_word = _{ WHITESPACE | separator | "|" | ">" | "<" } 

word = @{ (!non_word ~ ANY)+ }
var = @{ LETTER ~ (LETTER | NUMBER)* }
fd = { NUMBER+ }
pipe = { "|" }
redirect_fd = ${ fd? ~ ">" ~ WHITESPACE? ~ fd }
redirect_write = ${ fd? ~ ">" ~ WHITESPACE? ~ word }
redirect_append = ${ fd? ~ ">>" ~ WHITESPACE? ~ word }
redirect_read = ${ fd? ~ "<" ~ WHITESPACE? ~ word }

redirect = { pipe | redirect_fd | redirect_append | redirect_write | redirect_read }

assign = ${ var ~ "=" ~ word }
execute = { (redirect | word)+ }

command = { (assign | execute) }
line = _{ SOI ~ command ~ (separator ~ command)* ~ ";"? ~ EOI }